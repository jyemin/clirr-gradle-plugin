import org.gradle.api.artifacts.maven.MavenDeployment

/*
 * Copyright (c) 2008 - 2013 10gen, Inc. <http://10gen.com>
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *   http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

group = 'me.trnl'
version = '0.4-SNAPSHOT'

sourceCompatibility = '1.6'
targetCompatibility = '1.6'

repositories{
    mavenCentral()
}

dependencies {
    compile gradleApi()
    compile 'net.sf.clirr:clirr-core:0.6'
    compile 'org.yaml:snakeyaml:1.12'
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

artifacts {
    archives sourcesJar
    archives javadocJar
}

signing {
    required { gradle.taskGraph.hasTask("uploadArchives") }
    sign configurations.archives
}

task wrapper(type: Wrapper) {
    gradleVersion = "1.6"
}

project.ext.mavenDeployers = []

def pom = { config ->
    project.mavenDeployers*.pom config
}

install {
    project.mavenDeployers << repositories.mavenInstaller
}

uploadArchives {
    project.mavenDeployers << repositories.mavenDeployer {
        beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }
        def sonatypeCredentials = [
                userName: project.properties.sonatypeUsername,
                password: project.properties.sonatypePassword
        ]

        snapshotRepository(url: 'https://oss.sonatype.org/content/repositories/snapshots') {
            authentication(sonatypeCredentials)
        }
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(sonatypeCredentials)
        }
    }
}

pom {
    project {

        name 'Clirr Gradle Plugin'
        description =
        '''
        Clirr is a tool that checks Java libraries for binary and source compatibility with older releases.'
        This plugin allows you to use Clirr from Gradle.
        '''
        url 'http://github.com/trnl/clirr-gradle-plugin'
        scm {
            url 'git@github.com:trnl/clirr-gradle-plugin.git'
            connection 'scm:git:git@github.com:trnl/clirr-gradle-plugin.git'
            developerConnection 'scm:git:git@github.com:trnl/clirr-gradle-plugin.git'

        }

        licenses {
            license {
                name 'The Apache Software License, Version 2.0'
                url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'trnl'
                name 'Uladzimir Mihura'
                url = 'http://github.com/trnl'
            }
        }
    }
}



